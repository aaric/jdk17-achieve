plugins {
    id "java"
    id "io.freefair.lombok" version "8.6"
    id "io.spring.dependency-management" version "1.1.4"
    id "team.yi.semantic-gitlog" version "0.6.12"
}

group = "com.sample"
version = "0.8.0-SNAPSHOT"

ext {
    hutoolVersion = "5.8.40"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    maven { url "https://maven.aliyun.com/repository/public" }
}

dependencies {
    implementation "org.slf4j:slf4j-api"
    implementation "org.slf4j:slf4j-simple"

    implementation "commons-beanutils:commons-beanutils"
    implementation "commons-codec:commons-codec"

    implementation "commons-net:commons-net"
    implementation "dev.samstevens.totp:totp"

    implementation "cn.hutool:hutool-core"
    implementation "cn.hutool:hutool-cache"

    implementation "org.mapstruct:mapstruct"
    annotationProcessor "org.mapstruct:mapstruct-processor"

    implementation "com.github.ben-manes.caffeine:caffeine"

    implementation "org.jaudiotagger:jaudiotagger"

    implementation "io.projectreactor:reactor-core"

    testImplementation("org.junit.jupiter:junit-jupiter")
    //testImplementation("org.hamcrest:hamcrest")
    testImplementation("org.assertj:assertj-core")
    testAnnotationProcessor "org.mapstruct:mapstruct-processor"

    testImplementation "mysql:mysql-connector-java"
    testImplementation "com.baomidou:mybatis-plus-core"
    testImplementation "com.baomidou:mybatis-plus-generator"
    testImplementation "org.springframework.boot:spring-boot-starter-freemarker"
}

dependencyManagement {
    imports {
        mavenBom "io.projectreactor:reactor-bom:2024.0.10"
    }

    dependencies {
        dependencySet(group: "org.slf4j", version: "1.7.36") {
            entry "slf4j-api"
            entry "slf4j-simple"
        }

        dependency "org.junit.jupiter:junit-jupiter:5.8.2"
        //dependency "org.hamcrest:hamcrest:2.2"
        dependency "org.assertj:assertj-core:3.25.3"

        dependency "commons-beanutils:commons-beanutils:1.9.4"
        dependency "commons-codec:commons-codec:1.16.0"

        dependency "commons-net:commons-net:3.9.0"
        dependency "dev.samstevens.totp:totp:1.7.1"

        dependency "cn.hutool:hutool-core:$hutoolVersion"
        dependency "cn.hutool:hutool-cache:$hutoolVersion"

        dependency "org.mapstruct:mapstruct:1.5.5.Final"
        dependency "org.mapstruct:mapstruct-processor:1.5.5.Final"

        dependency "com.github.ben-manes.caffeine:caffeine:3.1.8"

        dependency "org.jaudiotagger:jaudiotagger:2.0.1"

        dependency "mysql:mysql-connector-java:8.0.33"
        dependency "com.baomidou:mybatis-plus-core:3.5.11"
        dependency "com.baomidou:mybatis-plus-generator:3.5.11"
        dependency "org.springframework.boot:spring-boot-starter-freemarker:3.4.4"
    }
}

// gradle myTask
def myTask = tasks.register("myTask") {
    doLast {
        print("myTask done")
    }
}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked"

    dependsOn myTask
}

compileTestJava {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked"
}

test {
    useJUnitPlatform()
}

changelog {
    toRef = "master"
    isUnstable = true

    jsonFile = file("${project.rootDir}/CHANGELOG.json")
    fileSets = [
            {
                template = file("${project.rootDir}/config/gitlog/CHANGELOG.mustache")
                target = file("${project.rootDir}/CHANGELOG.md")
            },
            {
                template = file("${project.rootDir}/config/gitlog/CHANGELOG.zh-cn.mustache")
                target = file("${project.rootDir}/CHANGELOG.zh-cn.md")
            }
    ]
    commitLocales = [
            "zh-cn": file("${project.rootDir}/config/gitlog/commit-locales.zh-cn.md")
    ]
}

derive {
    toRef = "master"
    isUnstable = true
    derivedVersionMark = "NEXT_VERSION:=="

    commitLocales = [
            "zh-cn": file("${project.rootDir}/config/gitlog/commit-locales.zh-cn.md")
    ]
}

task removeLombokConfig() {
    def subProjectPath = project.projectDir
    if (null != subProjectPath) {
        def lombokConfigFile = new File(subProjectPath, "lombok.config")
        if (lombokConfigFile.exists()) {
            lombokConfigFile.delete()
        }
    }
}

task removeOutputLog() {
    def subProjectPath = project.projectDir
    if (null != subProjectPath) {
        def logPath = new File(subProjectPath, "output.log")
        if (logPath.exists()) {
            logPath.delete()
        }
    }
}

defaultTasks "assemble"